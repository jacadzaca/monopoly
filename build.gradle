plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.70'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
  id "org.jetbrains.kotlin.kapt" version '1.3.70'
}

group = 'com.jacadzaca'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
  jcenter()
}

ext {
  kotlinVersion = '1.3.70'
  vertxVersion = '3.9.0'
  junitJupiterEngineVersion = '5.4.0'
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}

sourceSets {
  main {
    resources {
      srcDirs "src/main/resources"
    }
  }
}

def mainVerticleName = 'com.jacadzaca.monopoly.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation "io.vertx:vertx-junit5:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-pg-client:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
  implementation "io.vertx:vertx-redis-client:$vertxVersion"
  implementation "io.vertx:vertx-service-proxy:$vertxVersion"
  kapt "io.vertx:vertx-codegen:$vertxVersion:processor"
  compileOnly "io.vertx:vertx-codegen:$vertxVersion"

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
  testImplementation "io.mockk:mockk:1.9.3"
}

compileKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

// https://stackoverflow.com/questions/44781041/vert-x-service-proxies-from-kotlin-with-vertx-codegen
task generateProxies(type: JavaCompile, group: "build",
  description: "Generates the Vert.x proxies") { // codegen
  source = sourceSets.main.java
  source += sourceSets.main.kotlin
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file("${projectDir}/src/main/generated")
  options.compilerArgs = [
    "-proc:only",
    "-processor", "io.vertx.codegen.CodeGenProcessor",
    "-Acodegen.output=${project.projectDir}/src/main"
  ]
}

shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}
